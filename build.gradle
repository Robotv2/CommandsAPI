plugins {
    id 'io.github.goooler.shadow' version '8.1.8'
    id 'java'
    id 'maven-publish'
}

group = 'fr.traqueur'
version = '1.0.1-DEBUG-5'

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT"
    implementation group: 'lol.hyper', name: 'github-release-api', version: '1.0.5'
}

task generateVersionProperties {
    doLast {
        def file = new File("$projectDir/src/main/resources/version.properties")
        if (!file.parentFile.exists()) {
            file.parentFile.mkdirs()
        }
        file.text = "version=${project.version}"
    }
}

processResources.dependsOn generateVersionProperties

shadowJar {
    relocate 'lol.hyper.githubreleaseapi', 'fr.traqueur.commands.updater'
    archiveClassifier.set('')
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.assemble {
    dependsOn(shadowJar)
}

publishing {
    publications {
        shadow(MavenPublication) {
            artifact shadowJar
        }
    }
}

wrapper {
    gradleVersion = "8.7"
    distributionType = Wrapper.DistributionType.ALL
}